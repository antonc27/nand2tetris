// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    // Instruction handling
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=outInst); 

    Not(in=instruction[15], out=loadAFromInstr); 
    Or(a=loadAFromInstr, b=loadAFromALUOut, out=loadA); 

    ARegister(in=outInst, load=loadA, out=outA, out[0..14]=addressM[0..14]); 

    // ALU operation: inputs
    DRegister(in=aluOut, load=loadDFromALUOut, out=outD); 

    Mux16(a=outA, b=inM, sel=instruction[12], out=aluInputAM); 

    ALU(x=outD, y=aluInputAM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=outZr, ng=outNg); 

    // ALU operation: outputs
    And(a=instruction[15], b=instruction[5], out=loadAFromALUOut); 
    And(a=instruction[15], b=instruction[4], out=loadDFromALUOut); 
    And(a=instruction[15], b=instruction[3], out=writeM); 

    // Jumps
    Not(in=outZr, out=notZr); 
    Not(in=outNg, out=notNg); 

    // jump j3 = 1, ng = 0, zr = 0 (x > 0)
    And(a=notZr, b=notNg, out=tmp1);
    And(a=instruction[0], b=tmp1, out=load1);

    // jump j2 = 1, ng = 0, zr = 1 (x == 0)
    And(a=outZr, b=notNg, out=tmp2);
    And(a=instruction[1], b=tmp2, out=load2);

    // jump j1 = 1, ng = 1, zr = 0 (x < 0)
    And(a=notZr, b=outNg, out=tmp3);
    And(a=instruction[2], b=tmp3, out=load3);

    Or(a=load1, b=load2, out=loadTmp);
    Or(a=loadTmp, b=load3, out=loadOut);

    And(a=instruction[15], b=loadOut, out=loadJmp);

    // Program counter
    PC(in=outA, load=loadJmp, inc=true, reset=reset, out[0..14]=pc[0..14]); 
}